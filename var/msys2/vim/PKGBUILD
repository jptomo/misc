# Maintainer: Alexey Pavlov <alexpux@gmail.com>
# vim:ts=2 sw=2 sts=2:
# required=('base-devel' 'binutils')

# update pkgver
pkgname=vim
_repourl='git://github.com/vim/vim.git'
_latest=`git ls-remote --tags ${_repourl} \
    | sed -e 's/.\+\t//g' \
    | grep 'refs/tags' \
    | sed -e 's/refs\/tags\///' \
    | grep 'v[0-9]\.[0-9]\.[0-9]\+' | sort -r | head -n 1`
pkgver=`printf ${_latest} | sed -e 's/v//' `
_versiondir="${pkgname}`printf $pkgver | sed -e 's/.[0-9]\+$//' | sed -e 's/\.//'`"

# main settings
pkgrel=8
arch=('i686' 'x86_64')
license=('custom:vim')
url="http://www.vim.org"
depends=('ncurses-devel')
groups=('editors')
makedepends=('git' 'gcc' 'autoconf' 'gawk' 'perl' 'python2' 'libiconv')
source=('dot.vimrc'
        '7.3-cygwin-mouse.patch'
        '7.3-virc.patch'
        '7.3-cygwin-python-dyn.patch'
        'pretend-cygwin-msys.patch'
        '7.4-output_osdef_h.patch'
        'vim-completion'
        'http://www.lua.org/ftp/lua-5.3.1.tar.gz')
md5sums=('b18bd005832117243727e9fef1b45691'
         '82e8c2ae0821d3a453c808e98916c4b8'
         'e776f485d8f027050ff040120acb8a86'
         '88ab80d81af8e88cf80d21b2796ec94d'
         'a5d13302787cd004ae188b69e353035d'
         'f22f36abf096aaea18320c309cef5c45'
         '87fe7821e180647f3bf48ed099a22b83'
         '797adacada8d85761c079390ff1d9961')
_luasrcdirbase='lua-5.3.1'

prepare() {
  # get lua and build it
  _luasrcdir="${srcdir}/${_luasrcdirbase}"
  if [ -d ${_luasrcdir} -a ${_luasrcdir} != / ] ; then
    mv ${_luasrcdir} ${_luasrcdir}_`date +%Y`_`date +%m%d`_`date +%H%M`_`date +%S`
  fi

  tar xf ${_luasrcdir}.tar.gz
  pushd ${_luasrcdir}
    sed -ie 's!^PLAT=.*!PLAT= mingw!g' ${_luasrcdir}/Makefile
    sed -ie 's!^INSTALL_TOP=.*!INSTALL_TOP= '"${_luasrcdir}"'/usr!g' ${_luasrcdir}/Makefile
    make
    make install
  popd

  # initilize vim src
  _workdir="${srcdir}/${pkgname}-${pkgver}"
  if [ -d ${_workdir} -a ${_workdir} != / ] ; then
    mv ${_workdir} ${_workdir}_`date +%Y`_`date +%m%d`_`date +%H%M`_`date +%S`
  fi

  git clone -q --depth 1 -b ${_latest} ${_repourl} ${pkgname}-${pkgver}

  if [ -d ${_workdir}/.git ] ; then
    rm -fr ${_workdir}/.git
  fi
  if [ -f ${pkgname}-${pkgver}/.gitignore ] ; then
    rm ${pkgname}-${pkgver}/.gitignore
  fi

  cd ${srcdir}/${pkgname}-${pkgver}

  iconv -f ISO-8859-1 -t UTF-8 runtime/doc/eval.txt \
    > runtime/doc/eval.tmp && mv -f runtime/doc/eval.{tmp,txt}

  patch -p2 -i ${srcdir}/7.3-cygwin-mouse.patch
  patch -p2 -i ${srcdir}/7.3-virc.patch
  patch -p2 -i ${srcdir}/7.3-cygwin-python-dyn.patch
  patch -p1 -i ${srcdir}/pretend-cygwin-msys.patch
  patch -p1 -i ${srcdir}/7.4-output_osdef_h.patch

  cd ${_workdir}/src
  make autoconf
}

build() {
  cd ${srcdir}/${pkgname}-${pkgver}/src

  ./configure \
    --prefix=/usr \
    --build=${CHOST} \
    --with-features=huge \
    --with-tlib=ncursesw \
    --enable-cscope \
    --enable-multibyte \
    --enable-luainterp=yes \
    --with-lua-prefix="${srcdir}/${_luasrcdirbase}/usr" \
    --enable-pythoninterp=dynamic \
    --disable-gpm --disable-sysmouse \
    --disable-gui --without-x \
    CPPFLAGS="${CPPFLAGS} -I/usr/include/ncursesw" \
    --with-compiledby="<alexpux@gmail.com>" \
    --enable-fail-if-missing

  # make auto/osdef.h
  make VIMRCLOC=/etc VIMRUNTIMEDIR=/usr/share/vim/${_versiondir} output_auto_osdef_h
  make VIMRCLOC=/etc VIMRUNTIMEDIR=/usr/share/vim/${_versiondir}
}

check() {
  # disable tests because they seem to freeze

  cd "${srcdir}"/${pkgname}-${pkgver}

  #make test
}

package() {
  cd "${srcdir}"/${pkgname}-${pkgver}
  make -j1 VIMRCLOC=/etc DESTDIR="${pkgdir}" install

  make install -j1 \
    VIMRCLOC=/etc VIMRUNTIMEDIR=/usr/share/vim/${_versiondir} \
    STRIP=: INSTALL_DATA="cp -L" INSTALL_DATA_R="cp -Lr" \
    DESTDIR="${pkgdir}"

  for y in vimdiff view rview rvim
  do
    cp -f ${pkgdir}/usr/bin/vim.exe ${pkgdir}/usr/bin/${y}
  done

  mkdir -p ${pkgdir}/usr/share/bash-completion/completions
  cp -f ${srcdir}/vim-completion ${pkgdir}/usr/share/bash-completion/completions/vim

  # delete some manpages
  find "${pkgdir}"/usr/share/man -type d -name 'man1' 2>/dev/null | \
    while read _mandir; do
    cd ${_mandir}
    rm -f ex.1 # provided by (n)vi
    rm -f evim.1    # this does not make sense if we have no GUI
  done

  # fix FS#17216
  sed -i 's|messages,/var|messages,/var/log/messages.log,/var|' \
    "${pkgdir}"/usr/share/vim/${_versiondir}/filetype.vim

  # patch filetype.vim for better handling of pacman related files
  sed -i "s/rpmsave/pacsave/;s/rpmnew/pacnew/;s/,\*\.ebuild/\0,PKGBUILD*,*.install/" \
    "${pkgdir}"/usr/share/vim/${_versiondir}/filetype.vim
  sed -i "/find the end/,+3{s/changelog_date_entry_search/changelog_date_end_entry_search/}" \
    "${pkgdir}"/usr/share/vim/${_versiondir}/ftplugin/changelog.vim

  # rc files
  install -Dm644 "${srcdir}"/dot.vimrc "${pkgdir}"/etc/skel/.vimrc

  # rgb.txt file
  install -Dm644 "${srcdir}"/vim-${pkgver}/runtime/rgb.txt \
    "${pkgdir}"/usr/share/vim/${_versiondir}/rgb.txt

  # license
  install -Dm644 "${srcdir}"/vim-${pkgver}/runtime/doc/uganda.txt \
    "${pkgdir}"/usr/share/licenses/${pkgname}/license.txt
}
